extern "C" {
    fn printString(&[u8]) -> ();
    fn printLine(&[u8]) -> ();
    fn printFloat(f32) -> ();
    fn printDouble(f64) -> ();
    fn printInteger(i32) -> ();
    fn log(f64) -> (f64);
    fn exp(f64) -> (f64);
    fn logf(f32) -> (f32);
    fn lgamma(f64) -> (f64);
    fn randomDouble(f64, f64) -> (f64);
}

fn range(mut b: u64, e: u64, body: fn(u64) -> ()) -> () {
    while b < e {
        body(b++)
    }
}

fn print_matrix(m : mat[2:f64]) -> (){
    printLine("----");
    for row in range(0 as u64, m.shape(0)){
        printString("|");
        for col in range(0 as u64, m.shape(1)){
            let value = m(row,col);
            if value >= 0.0{
                printString("");
            }
            printDouble(value);
        }

        printLine("|");
    }
    printLine("----");
}

fn iter( m: mat[2:f64], body: fn((u64, u64,  &mut f64)) -> ()) -> () {
    let mut row = 0 as u64;
    let rows = m.shape(0);
    let cols = m.shape(1);
    while row < rows {
        let mut col = 0 as u64;
        while col < cols {
            body((row, col, (&(m(row, col))) as &mut f64));
            col++;
        }
        row++;
    }
}

//(ptr, f64, ...)

fn matrix_init(x : mat[2:f64], offset: f64) -> (){
    let cols = x.shape(1);
    for cell in iter( x ){
        let (row, col, ptr) = cell;
        *ptr = (row * cols + col) as f64 + offset;
    }
}

fn matrix_init_random(x : mat[2:f64], min: f64, max: f64) -> (){
    let cols = x.shape(1);
    for cell in iter( x ){
        let (row, col, ptr) = cell;
        *ptr = randomDouble(min, max);
    }
}

fn @(true) softmax(input: mat[2:f64]) -> (mat[2:f64]){
  let e = input.map(exp);
  e / e.sum
}

fn relu( x: f64 ) -> (f64){
    if(x>=0.0){
        x
    }else{
        0.1 * x
    }
}

fn predict(input: mat[2:f64], first_weights: mat[2:f64], second_weights: mat[2:f64]) -> mat[2:f64]{
    let first_layer = (first_weights * input).map(relu);
    let second_layer = (second_weights * first_layer).map(relu);
    let classification = softmax(second_layer);
    classification
}

fn loss(input: mat[2:f64], first_weights: mat[2:f64], second_weights: mat[2:f64], y: u64) -> f64{
    let classification = predict(input, first_weights, second_weights);
    -log(classification(0,y))
}

fn main() -> i32 {
    let dim = 10;

    let input : mat[2:f64] = mat[f64](28*28, 1);
    let mut first_weights : mat[2:f64] = mat[f64](32, 28*28);
    let mut second_weights : mat[2:f64] = mat[f64](10, 32);

    matrix_init_random(input, 0.0, 1.0);
    matrix_init_random(first_weights, -0.1, 0.1);
    matrix_init_random(second_weights, -0.1, 0.1);
    let Df = rev_diff(loss);

    let mut i = 0;
    while i < 20{
        let (fx,pb) = Df(input, first_weights, second_weights, 4 as u64);
        let gradients = pb(1.0);
        let input_d = gradients(0);
        let first_weights_d = gradients(1);
        let second_weights_d = gradients(2);

        first_weights = first_weights - 0.1 * first_weights_d;
        second_weights = second_weights - 0.1 * second_weights_d;

        printDouble(fx);
        printLine("");
        i++;
    }

    let prediction = predict(input, first_weights, second_weights);
    print_matrix(prediction);

    0
}
